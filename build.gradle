buildscript {
    ext {
        gradleVersion = '5.6.2'
        jdkVersion = JavaVersion.VERSION_1_8
        // jdkVersion = JavaVersion.VERSION_11
        junitVersion = '5.4.2'
        assertJVersion = '3.11.1'
        defaultEncoding = 'UTF-8'
        flexMarkJavaVersion = '0.50.40'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
    }
}

wrapper {
    gradleVersion = "${gradleVersion}"
}

apply {
    plugin 'java'
    plugin 'java-library'
    plugin 'eclipse'
}

[compileJava, compileTestJava]*.options*.encoding = "${defaultEncoding}"
sourceCompatibility = "${jdkVersion}"
targetCompatibility = "${jdkVersion}"
sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test.compileClasspath += configurations.compileOnly
    test.runtimeClasspath += configurations.compileOnly
}
test {
    useJUnitPlatform()
}
javadoc {
    options.charSet = "${defaultEncoding}"
    options.encoding = "${defaultEncoding}"
}

task initSrcDirs {
    doLast {
        sourceSets.all {
            java.srcDirs*.mkdirs()
        }
    }
}
tasks.eclipse.dependsOn(initSrcDirs)

repositories {
    mavenCentral()
}
dependencies {
    implementation gradleApi()
    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation "org.assertj:assertj-core:${assertJVersion}"

    implementation("com.vladsch.flexmark:flexmark:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-media-tags:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-tables:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-definition:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-typographic:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-toc:${flexMarkJavaVersion}")
    implementation("com.vladsch.flexmark:flexmark-ext-aside:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Usage#include-markdown-and-html-file-content
    implementation("com.vladsch.flexmark:flexmark-ext-jekyll-tag:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Admonition-Extension
    implementation("com.vladsch.flexmark:flexmark-ext-admonition:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Extensions#anchorlink
    implementation("com.vladsch.flexmark:flexmark-ext-anchorlink:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Attributes-Extension
    implementation("com.vladsch.flexmark:flexmark-ext-attributes:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Extensions#footnotes
    implementation("com.vladsch.flexmark:flexmark-ext-footnotes:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Extensions#gfm-tasklist
    implementation("com.vladsch.flexmark:flexmark-ext-gfm-tasklist:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Extensions#gitlab-flavoured-markdown
    implementation("com.vladsch.flexmark:flexmark-ext-gitlab:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/Extensions#macros
    implementation("com.vladsch.flexmark:flexmark-ext-macros:${flexMarkJavaVersion}")

    // https://github.com/vsch/flexmark-java/wiki/Extensions#docx-converter
    // https://github.com/vsch/flexmark-java/wiki/Customizing-Docx-Rendering
    // implementation("com.vladsch.flexmark:flexmark-docx-converter:${flexMarkJavaVersion}")
    // https://github.com/vsch/flexmark-java/wiki/PDF-Renderer-Converter
    implementation("com.vladsch.flexmark:flexmark-pdf-converter:${flexMarkJavaVersion}")
}

version = '0.0.1'
group = 'com.matsuyoido'
jar.baseName = 'matsuyoido-markdown-gradle-plugin'

apply plugin: 'java-gradle-plugin'
gradlePlugin {
    plugins {
        mdPlugin {
            id = 'com.matsuyoido.markdown'
            implementationClass = 'com.matsuyoido.plugin.markdown.MainPlugin'
            displayName = 'This plugin support preparation of document.'
            description = 'md -> html | md -> pdf'
        }
    }
}

apply plugin: 'com.gradle.plugin-publish'
pluginBundle {
    website = 'https://github.com/matsuyoido/gradle-md-plugin' // temporary url
    vcsUrl = 'https://github.com/matsuyoido/gradle-md-plugin'
    tags = ['document', 'md', 'markdown', 'pdf', 'html']
}